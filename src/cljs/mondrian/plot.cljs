(ns mondrian.plot
  (:use [monet.canvas :only [fill-style circle]]))

(defn parametric-equation
  "Plots the parametric equation whose points are generated by calling
  fx and fy on each value (in radians) to be plotted.  fcolor also takes
  a value in radians and is called to generate a color value for each
  point.  num-points indicates the total number of points that should be
  plotted; each point will be spaced evenly around the unit circle.
  point-size is the size, in pixels, of each point.  Returns the drawing
  context (for use in threading)."
  [ctx fx fy fcolor num-points point-size]
  (let [point-step (/ (* 2 Math/PI) num-points)]
    (dotimes [index num-points]
      (let [point-rad (* index point-step)]
        (-> ctx
            (fill-style (fcolor point-rad))
            (circle {:x (fx point-rad) :y (fy point-rad) :r point-size})))))
  ctx)
